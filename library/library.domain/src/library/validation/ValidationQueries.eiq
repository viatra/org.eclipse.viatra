/*******************************************************************************
 * Copyright (c) 2010-2013, Tamas Szabo, Istvan Rath and Daniel Varro
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *   Tamas Szabo - initial API and implementation
 *   Istvan Rath - minor modifications
 *******************************************************************************/
 package library.validation

import "library"

// Validation queries
 
 /*
 * A "suspicious" book is one that:
 *  - is empty (missing some important field)
 *  - is weird (it transitively cites itself)
 */
@Item(item = B, label = "Suspicious book $B.title$")
pattern suspiciousBook(L:Library, B: Book) {
	Library.books(L,B);
	find emptyBook(B);
} 
or {
	Library.books(L,B);
	find weirdBook(B);
}

/*
 * B is an empty book because it has either no title and/or no authors.
 */
pattern emptyBook(B: Book) {
	Book.title(B, T);
	check((T as String).length == 0);
} 
or {
	Book.numberOfWriters(B,0);
}

/*
 * B is a weird Book because it transitively cites itself.
 */
pattern weirdBook(B : Book) {
	Book.allCitations(B, B);
}