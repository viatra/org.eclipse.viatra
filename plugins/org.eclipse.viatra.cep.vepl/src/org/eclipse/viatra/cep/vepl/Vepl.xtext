/*******************************************************************************
 * Copyright (c) 2004-2014, Istvan David, Istvan Rath and Daniel Varro
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 * Istvan David - initial API and implementation
 *******************************************************************************/

grammar org.eclipse.viatra.cep.vepl.Vepl with org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/incquery/patternlanguage/PatternLanguage" as iqpl
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvmTypes
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate vepl "http://www.eclipse.org/viatra/cep/vepl/Vepl"

EventModel:
	'package' name=QualifiedName
	imports+=Import*
	modelElements+=ModelElement*	
;

DOUBLE returns ecore::EDouble:
	('-')? INT('.' INT)?;

Import:
	GenericImport | QueryImport
;

GenericImport:
	'import' importedNamespace=QualifiedNameWithWildcard;
	
QueryImport:
	'import-queries' importedNamespace=QualifiedNameWithWildcard;

ModelElement:
	EventPattern | Rule | Source
;

/*******************************************MAIN MODEL ELEMENTS*******************************************/

EventPattern:
	AbstractAtomicEventPattern | ComplexEventPattern
;

AbstractAtomicEventPattern:
	AtomicEventPattern | QueryResultChangeEventPattern
;
	
AtomicEventPattern:
	'AtomicEvent' name=ID ('('parameters=TypedParameterList?')')? ('{'
		('source' ':' source=[Source])?
		('staticBindings' staticBindings = XBlockExpression)?
	'}')?
;
	
QueryResultChangeEventPattern:
	'QueryResultChangeEvent' name=ID '('parameters=TypedParameterList?')' '{'
		'query' ':' queryReference=ParametrizedQueryReference
		('resultChangeType' ':' resultChangeType=QueryResultChangeType)?
	'}'
;

ComplexEventPattern:
	'ComplexEvent' name=ID '('parameters=TypedParameterList?')' '{'
		('definition'':'complexEventExpression=ComplexEventExpression)?
	'}'
;

Rule:
	'Rule' name=ID '{'
		'events' ':' eventPatterns+=ParameterizedPatternCall (','eventPatterns+=ParameterizedPatternCall)*
		('actionHandler' ':' actionHandler=QualifiedName)?
		('action' action = XBlockExpression)?
	'}'
;


/*******************************************BASIC EVENT TYPE ELEMENTS*******************************************/
	
	
	
/*******************************************IQ PATTERN ELEMENTS*******************************************/
enum QueryResultChangeType:
	NEW_MATCH_FOUND | EXISTING_MATCH_LOST 
;
/*******************************************IQ PATTERN ELEMENTS*******************************************/



/*******************************************FUNCTION ARGUMENT PARAMETERS*******************************************/
TypedParameterList:
	{TypedParameterList}
	parameters+=TypedParameter (',' parameters+=TypedParameter)*
;

TypedParameter:
	{TypedParameter}
	name=ID ':' type = JvmTypeReference
;
/*******************************************FUNCTION ARGUMENT PARAMETERS*******************************************/

/*******************************************INCQUERY INTEGRATION*******************************************/
ParametrizedQueryReference:
	query=[iqpl::Pattern]('('parameterList=PatternCallParameterList?')')?
;
/*******************************************INCQUERY INTEGRATION*******************************************/

/*******************************************COMPLEX EVENT EXPRESSIONS*******************************************/

ComplexEventExpression:
	Primary ({ComplexEventExpression.left=current} (right+=ChainedExpression)*)
;

ChainedExpression:
	operator=ComplexEventOperator expression=Primary
;

Primary returns ComplexEventExpression:
	Atom (multiplicity=AbstractMultiplicity)? (timewindow=Timewindow)? | '('ComplexEventExpression')' (multiplicity=AbstractMultiplicity)? (timewindow=Timewindow)?
;

Atom:
	patternCall=ParameterizedPatternCall
;

AbstractMultiplicity:
	Multiplicity | Infinite | AtLeastOne
;

Multiplicity returns AbstractMultiplicity:
	{Multiplicity}
	'{'value=INT'}'
;

Infinite returns AbstractMultiplicity:
	{Infinite}
	'{''*''}'
;

AtLeastOne returns AbstractMultiplicity:
	{AtLeastOne}
	'{''+''}'
;

Timewindow:
	'['length=INT']'
;

ParameterizedPatternCall:
	eventPattern=[EventPattern]('('parameterList=PatternCallParameterList?')')?
;

PatternCallParameterList:
	{PatternCallParameterList}
	parameters+=PatternCallParameter (',' parameters+=PatternCallParameter)*
;

PatternCallParameter:
	{PatternCallParameter}
	name=ID
;

NegExpression:
	{NegExpression}
	operator=NegOperator '('eventPattern=EventPattern')'
;

/*******************************************COMPLEX EVENT EXPRESSIONS*******************************************/



/*******************************************OPERATORS*******************************************/
ComplexEventOperator:
	FollowsOperator | OrOperator | UntilOperator | AndOperator //| NegOperator
;

FollowsOperator:
	{FollowsOperator}
	'->'
;

OrOperator:
	{OrOperator}
	'OR'
;

AndOperator:
	{AndOperator}
	'AND'
;

UntilOperator:
	{UntilOperator}
	'U'
;

NegOperator:
	{NegOperator}
	'NEG'
;

/*******************************************OPERATORS*******************************************/


/*******************************************STRUCTURAL/INTEGRATION FEATURES*******************************************/
Source:
	'Source' name=ID '{'
		('register' adapter+=Adapter)+
	'}'
;

Adapter:
	name=QualifiedName
;
/*******************************************STRUCTURAL/INTEGRATION FEATURES*******************************************/