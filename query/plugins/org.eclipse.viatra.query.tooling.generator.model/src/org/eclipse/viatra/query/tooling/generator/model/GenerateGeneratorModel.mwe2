/*******************************************************************************
 * Copyright (c) 2010-2012, Zoltan Ujhelyi, Istvan Rath and Daniel Varro
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License v. 2.0 which is available at
 * http://www.eclipse.org/legal/epl-v20.html.
 * 
 * SPDX-License-Identifier: EPL-2.0
 *******************************************************************************/
module org.eclipse.viatra.query.tooling.generator.model.GeneratorModel

import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var file.extensions = "vqgen"
var rootPath = ".."
var projectName = "org.eclipse.viatra.query.tooling.generator.model"

Workflow {
    bean = StandaloneSetup {
            scanClassPath = true

            registerGeneratedEPackage = "org.eclipse.emf.codegen.ecore.genmodel.GenModelPackage"
            registerGenModelFile = "platform:/resource/org.eclipse.emf.codegen.ecore/model/GenModel.genmodel"
        }
    
        component = XtextGenerator {
                    configuration = {
            project = StandardProjectConfig {
                baseName = projectName
                rootPath = rootPath
                
                runtime = {
                       root="${rootPath}/${projectName}"
                }
                runtimeTest = {
                    enabled = false
                }
                genericIde = {
                    root = "${rootPath}/${projectName}.ui"
                }
                eclipsePlugin = {
                    enabled = true
                    root = "${rootPath}/${projectName}.ui"
                    pluginXml = {
                        path = "plugin.xml_gen"
                    }
                    manifest = {
                        path = "MANIFEST.MF_gen" 
                    }
                }
                eclipsePluginTest = {
                    enabled = false
                }

                mavenLayout = false
                createEclipseMetaData = false
            }
            
            code = {
                encoding = "UTF-8"
                fileHeader = "/*******************************************************************************
 * Copyright (c) 2010-2018, Zoltan Ujhelyi, Istvan Rath and Daniel Varro
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License v. 2.0 which is available at
 * http://www.eclipse.org/legal/epl-v20.html.
 * 
 * SPDX-License-Identifier: EPL-2.0
 *
 * This file was generated from GeneratorModel.xtext
 *******************************************************************************/"
                preferXtendStubs = false
                
            }
            
            
        }
        
        language = XtextGeneratorLanguage {
            name = "org.eclipse.viatra.query.tooling.generator.model.GeneratorModel"
            fileExtensions = file.extensions
            
            fragment = grammarAccess.GrammarAccessFragment2 {}
            fragment = ecore.EMFGeneratorFragment2 {
                jdkLevel = "JDK80"
            }
            fragment = serializer.SerializerFragment2 {
                generateStub = false
            }
            fragment = resourceFactory.ResourceFactoryFragment2 {}
            fragment = parser.antlr.XtextAntlrGeneratorFragment2 {}
            fragment = validation.ValidatorFragment2 {}
            fragment = scoping.ImportNamespacesScopingFragment2 {}
            fragment = exporting.QualifiedNamesFragment2 {}
            fragment = ui.labeling.LabelProviderFragment2 {}
            fragment = ui.outline.QuickOutlineFragment2 {}
            fragment = ui.outline.OutlineTreeProviderFragment2 {}
            fragment = ui.quickfix.QuickfixProviderFragment2 {}
            fragment = ui.contentAssist.ContentAssistFragment2 {}
            fragment = ui.refactoring.RefactorElementNameFragment2 {}
            fragment = ui.compare.CompareFragment2 {}
        }
    }
        
}

