package org.eclipse.viatra.query.tooling.ui.retevis

import "http://www.eclipse.org/viatra/query/rete/recipes"

@Item(item = recipe, label = "Input: $recipe.inputKey$")
@Format(textColor = "#FFFFFF", color = "#7F004D")
pattern inputRecipe(recipe : InputRecipe) {
	InputRecipe(recipe);
}

@Item(item = recipe, label = "Constant: $recipe.constantValues$")
@Format(textColor = "#FFFFFF", color = "#7F004D")
pattern constantRecipe(recipe : ConstantRecipe) {
	ConstantRecipe(recipe);
}

@Item(item = recipe, label = "Count")
pattern countAggregatorRecipe(recipe : CountAggregatorRecipe) {
	CountAggregatorRecipe(recipe);
}

@Item(item = recipe, label = "Anti-join")
pattern antiJoinRecipe(recipe : AntiJoinRecipe) {
	AntiJoinRecipe(recipe);
}

@Item(item = recipe, label = "Semi-join")
pattern semiJoinRecipe(recipe : SemiJoinRecipe) {
	SemiJoinRecipe(recipe);
}

@Item(item = recipe, 
	label = "Join left + $rightParentComplementaryMask.sourceIndices$/$rightParentComplementaryMask.sourceArity$"
)
pattern joinRecipe(recipe : JoinRecipe, rightParentComplementaryMask : Mask) {
	JoinRecipe(recipe);
	JoinRecipe.rightParentComplementaryMask(recipe, rightParentComplementaryMask);
}

@Item(item = recipe, label = "Production: $recipe.patternFQN$")
@Format(textColor = "#FFFFFF", color = "#881A71")
pattern productionRecipe(recipe : ProductionRecipe) {
	ProductionRecipe(recipe);
}

@Item(item = recipe, label = "Uniqueness Enforcer")
pattern uniquenessEnforcerRecipe(recipe : UniquenessEnforcerRecipe) {
	UniquenessEnforcerRecipe(recipe);
}

@Item(item = recipe, label = "Check (cache=$cache$)")
pattern checkRecipe(recipe : CheckRecipe, cache: java Boolean) {
	CheckRecipe.cacheOutput(recipe, cache);
}
@Item(item = recipe, label = "Eval (cache=$cache$)")
pattern evalRecipe(recipe : EvalRecipe, cache: java Boolean) {
	EvalRecipe.cacheOutput(recipe, cache);
}

@Item(item = recipe, label = "InequalityFilter: $recipe.subject$ != $recipe.inequals$")
pattern inequalityFilterRecipe(recipe : InequalityFilterRecipe) {
	InequalityFilterRecipe(recipe);
}

@Item(item = recipe, label = "InputFilter: $recipe.inputKey$ @ $mask.sourceIndices$/$mask.sourceArity$")
@Format(textColor = "#7F004D", color = "#CCCCCC")
pattern inputFilterRecipeWithMask(recipe : InputFilterRecipe, mask : Mask) {
	InputFilterRecipe(recipe);
	find inputFilterMask(recipe, mask);
}
@Item(item = recipe, label = "InputFilter: $recipe.inputKey$")
@Format(textColor = "#7F004D", color = "#CCCCCC")
pattern inputFilterRecipeWithoutMask(recipe : InputFilterRecipe) {
	InputFilterRecipe(recipe);
	neg find inputFilterMask(recipe, _mask);
}
pattern inputFilterMask(recipe : InputFilterRecipe, mask : Mask) {
	InputFilterRecipe.mask(recipe, mask);
}

@Item(item = recipe, label = "EqualityFilter: $recipe.indices$")
pattern equalityFilterRecipe(recipe : EqualityFilterRecipe) {
	EqualityFilterRecipe(recipe);
}

@Item(item = recipe, label = "Transparent")
pattern transparentRecipe(recipe : TransparentRecipe) {
	TransparentRecipe(recipe);
}

@Item(item = recipe, label = "Dispatch on $colIndex$")
pattern discriminatorDispatcherRecipe(recipe : DiscriminatorDispatcherRecipe, colIndex : java Integer) {
	DiscriminatorDispatcherRecipe.discriminationColumnIndex(recipe, colIndex);
}

@Item(item = recipe, label = "Bucket: '$keyStr$'")
pattern discriminatorBucketRecipe(recipe : DiscriminatorBucketRecipe, keyStr : java String) {
	DiscriminatorBucketRecipe.bucketKey(recipe, key);
	keyStr == eval(key.toString);
}



@Item(item = recipe, label = "Transitive Closure")
pattern transitiveClosureRecipe(recipe : TransitiveClosureRecipe) {
	TransitiveClosureRecipe(recipe);
}

@Item(item = recipe, label = "Trimmer: $mask.sourceIndices$/$mask.sourceArity$")
pattern trimmerRecipe(recipe : TrimmerRecipe, mask : Mask) {
	TrimmerRecipe(recipe);
	TrimmerRecipe.mask(recipe, mask);
}

@Item(item = recipe, label = "$opName$($aggregableIndex$) by group $mask.sourceIndices$/$mask.sourceArity$")
pattern aggregatorRecipe(recipe : SingleColumnAggregatorRecipe, aggregableIndex : java Integer, mask : Mask, opName : java String) {
	SingleColumnAggregatorRecipe.aggregableIndex(recipe, aggregableIndex);
	SingleColumnAggregatorRecipe.groupByMask(recipe, mask);
	SingleColumnAggregatorRecipe.multisetAggregationOperator(recipe, operator);
	opName == eval(operator.name);
}

@Item(item = recipe, label = "Aggregator Indexer: $mask.sourceIndices$/$mask.sourceArity$")
@Format(textColor = "#000000", color = "#FFFFFF")
pattern aggregatorIndexerRecipe(recipe : AggregatorIndexerRecipe, mask : Mask) {
	AggregatorIndexerRecipe(recipe);
	AggregatorIndexerRecipe.mask(recipe, mask);
}

@Item(item = recipe, label = "IX: $mask.sourceIndices$/$mask.sourceArity$")
@Format(textColor = "#000000", color = "#FFFFFF")
pattern projectionIndexerRecipe(recipe : ProjectionIndexerRecipe, mask : Mask) {
	ProjectionIndexerRecipe(recipe);
	ProjectionIndexerRecipe.mask(recipe, mask);
}

@Edge(source = parent, target = recipe)
pattern aggregatorRecipeParent(recipe: IndexerBasedAggregatorRecipe, parent: ProjectionIndexerRecipe) {
	IndexerBasedAggregatorRecipe.parent(recipe, parent);	
}

@Edge(source = parent, target = recipe, label = "left")
pattern betaRecipeLeftParent(recipe: BetaRecipe, parent: ProjectionIndexerRecipe) {
	BetaRecipe.leftParent(recipe, parent);	
}

@Edge(source = parent, target = recipe, label = "right")
pattern betaRecipeRightParent(recipe: BetaRecipe, parent: IndexerRecipe) {
	BetaRecipe.rightParent(recipe, parent);	
}

@Edge(source = parent, target = recipe)
pattern multiParentNodeRecipeParents(recipe: MultiParentNodeRecipe, parent: ReteNodeRecipe) {
	MultiParentNodeRecipe.parents(recipe, parent);	
}

@Edge(source = parent, target = recipe)
pattern singleParentNodeRecipeParent(recipe: SingleParentNodeRecipe, parent: ReteNodeRecipe) {
	SingleParentNodeRecipe.parent(recipe, parent);	
}