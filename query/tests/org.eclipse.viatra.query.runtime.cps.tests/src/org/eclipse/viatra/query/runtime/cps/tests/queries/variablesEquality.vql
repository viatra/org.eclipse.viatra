/*******************************************************************************
 * Copyright (c) 2010-2016, Tamas Borbas, IncQuery Labs Ltd.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 * 
 * Contributors:
 *   Tamas Borbas - initial API and implementation
 *******************************************************************************/
package org.eclipse.viatra.query.runtime.cps.tests.queries
// This tests 481263 and 491248 bugs

import "http://org.eclipse.viatra/model/cps"

pattern sameVariables(a1, a2) {
	HostType(a1);
	HostType(a2);
	a1 == a2;
}

pattern enumNotEqual(a, s) {
    ApplicationInstance.state(a, s);
    s != AppState::Stopped;
}


private pattern oneInstance(at: ApplicationType){
    1 == count find applicationInstancesOfApplicationType(_ai, at);
}

/*
 * https://bugs.eclipse.org/bugs/show_bug.cgi?id=496989
 */
pattern notOneInstance(at: ApplicationType){
    neg find oneInstance(at);
}

pattern cartesianProduct(a1, a2){
    ApplicationType(a1);
    ApplicationType(a2);
}

pattern argumentsUnifiedByCaller(a){
    find cartesianProduct(a, a);
}